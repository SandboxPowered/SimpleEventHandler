plugins {
    id 'java-library'
    id 'maven-publish'
    id 'me.qoomon.git-versioning' version '3.0.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = 'eventhandler'
group 'org.sandboxpowered'
def ENV = System.getenv()
def releasesRepoUrl = "https://nexus.sandboxpowered.org/repository/maven-releases/"
def snapshotsRepoUrl = "https://nexus.sandboxpowered.org/repository/maven-snapshots/"

def release = "2.1.0"

version = "${release}-SNAPSHOT"

gitVersioning.apply {
    branch {
        pattern = 'develop'
        versionFormat = '${version}'
    }
    branch {
        pattern = 'release'
        versionFormat = '${version.release}'
    }
    branch {
        pattern = 'feature/(?<feature>.+)'
        versionFormat = '${feature}-SNAPSHOT'
    }
    tag {
        pattern = 'release/(?<tagVersion>[0-9].*)'
        versionFormat = '${tagVersion}'
    }
    commit {
        versionFormat = '${commit.short}'
    }
}
repositories {
    maven { url 'https://nexus.sandboxpowered.org/repository/maven-public/' }
}

dependencies {
    implementation 'com.google.guava:guava:21.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact jar
        }
    }

    repositories {
        maven {
            url = rootProject.version.endsWith("-SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username ENV.MAVEN_USER
                password ENV.MAVEN_PASS
            }
        }
    }
}
